isEmpty(DTK_BUILD): error("Please load(dtk_build) first!")
isEmpty(CMAKE_MODULE): error("load(dtk_cmake) before this!")

NAME_ID = $$DTK_MODULE_NAME
# 配置qt module 多版本的安装目录
MODULE_LIST = $${system(ls $$_PRO_FILE_PWD_ | grep -P qt_lib_$${NAME_ID}[a-z0-9._]+\.pri)}

multiversion_module=
for(QT_MODULE, MODULE_LIST) {
    multiversion_module.files += $$QT_MODULE
}

isEmpty(MKSPECS_INSTALL_DIR) {
    MKSPECS_INSTALL_DIR=$$[QT_INSTALL_ARCHDATA]/mkspecs
}
multiversion_module.path = $${MKSPECS_INSTALL_DIR}/modules
!isEmpty(MODULE_LIST): INSTALLS += multiversion_module


# 手动将PKG-CONFIG文件指定安装目录
PKG_INSTALL_PATH = $$LIB_INSTALL_DIR/pkgconfig
PKG_FILES_PATH = $${_PRO_FILE_PWD_}/pkgconfig

exists($$PKG_FILES_PATH) {
    pkg_files = $${system(ls $$PKG_FILES_PATH | grep -P $${NAME_ID}[a-z0-9._]+\.pc)}

    multi_pkg_module=
    for(pkg_file, pkg_files) {
        file_path = $${PKG_FILES_PATH}/$${pkg_file}
        multi_pkg_module.files += $$file_path
    }

    multi_pkg_module.path = $$PKG_INSTALL_PATH
    !isEmpty(pkg_files): INSTALLS += multi_pkg_module
}

